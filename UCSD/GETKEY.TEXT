;GETKEY FUNCTION
;SCAN THE KEYBOARD AND CHECK FOR ANY KEY PRESSED
;CLEARS THE KEYBOARD BUFFER AFTER EVERY SCAN
;RETURNS EITHER KEY VALUE OR >FF IF NO KEY IS PRESSED
;RETURNS UPPER CASE CODES REGARDLESS OF CAPS LOCK STATUS
;USAGE: N := GETKEY
;BY WALID MAALOULI. BASED ON CODE FROM ANDERS PERSSON
;JANUARY 2023

;MEMORY LOCATIONS DEFINITIONS
          .ASECT
          .ORG     8374H
KEYBOARD  .BYTE                 ;KEYBOARD SELECTION BYTE FOR SCAN ROUTINE
KEYVALUE  .BYTE                 ;RETURNED KEY VALUE BYTE
          .PSECT
          
PASCALWS  .EQU    8380H
GPLWS     .EQU    83E0H
SP        .EQU    10            ;R10 IS THE FUNCTION STACK POINTER
SAVEPAD   .EQU    83C6H         ;ADDRESS OF PAD AREA TO SAVE
STDVDPR1  .EQU    83D4H         ;STANDARD LOCATION OF VR1
PASVDPR1  .EQU    2811H         ;PASCAL LOCATION OF VR1
SCAN      .EQU    000EH         ;ADDRESS OF ROM SCAN ROUTINE

;KEYBOARD BUFFER VALUES
BUFEMPTY  .EQU    2D9EH         ;BUFFER EMPTY BIT MASK
BUFFULL   .EQU    2DA0H         ;BUFFER FULL BIT MASK
BUFFLAG   .EQU    28A2H         ;BUFFER FLAG.FF40=EMPTY,FF00=NOT EMPTY,FF80=FULL
BUFSTORE  .EQU    28A4H         ;BUFFER STORE POINTER
BUFGET    .EQU    28A6H         ;BUFFER FETCH POINTER

          .RELFUNC GETKEY

          CLR     @KEYBOARD     ;SELECT THE ENTIRE KEYBOARD FOR SCANNING
          
          LWPI    SCANWS        ;SAVE SOME OF THE RAM PAD LOCATIONS
          LI      R0,SAVEPAD
          MOV     *R0+,R1
          MOV     *R0+,R2
          MOV     *R0,R3
          LI      R0,STDVDPR1
          MOV     *R0+,R4
          MOV     *R0+,R5
          MOV     *R0,R6
          
          MOVB    @PASVDPR1,@STDVDPR1 ;RELOCATE THE PASCAL VR1 TO STD LOCATION
          LWPI    GPLWS         ;EXECUTE THE ROM SCAN ROUTINE
          BL      @SCAN
          
          LWPI    SCANWS        ;RESTORE RAM PAD VALUES
          LI      R0,SAVEPAD
          MOV     R1,*R0+
          MOV     R2,*R0+
          MOV     R3,*R0
          LI      R0,STDVDPR1
          MOV     R4,*R0+
          MOV     R5,*R0+
          MOV     R6,*R0
          
          LWPI    PASCALWS      ;GET KEY VALUE
          CLR     R0
          MOVB    @KEYVALUE,R0
          SWPB    R0
          MOV     R0,*SP        ;PLACE KEY VALUE ON FUNCTION RETURN STACK
          
          SZC     @BUFFULL,@BUFFLAG ;EMPTY KEYBOARD BUFFER
          SOC     @BUFEMPTY,@BUFFLAG
          MOV     @BUFGET,@BUFSTORE
          
          B       *R11
          
SCANWS    .BLOCK  32
          .END

